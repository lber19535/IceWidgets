apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'org.fabiomsr.drawableoptimizer'
apply plugin: 'bugly'

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.bill.icewidgets"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 5
        versionName "1.0." + versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        renameApk("IceWidgets-" + versionName)
    }
    // This is handled for you by the 2.0+ Gradle Plugin
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    signingConfigs {
        preview {
            storeFile file("../keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "IceWidgets"
            resValue "string", "app_version_name", "${defaultConfig.versionName}-release"
        }
        preview {
            applicationIdSuffix ".preview"
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.preview
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "IceWidgets-preview"
            resValue "string", "app_version_name", "${defaultConfig.versionName}-preview"
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "IceWidgets-debug"
            resValue "string", "app_version_name", "${defaultConfig.versionName}-debug"
        }
    }

    dataBinding {
        enabled = true
    }
}

drawableOptimizer {
    optimizer = 'pngquant'
    compressionLevel = 9
    iterations = 15
    onlyOnRelease = false
    logLevel = 'info'
}

bugly {
    appId = 'eae17414d4' // 注册时分配的App ID
    appKey = '2c398d83-1122-4c0a-8c06-c924d4524bd8' // 注册时分配的App Key
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:gridlayout-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.parse.bolts:bolts-tasks:1.4.0'
    compile 'com.parse.bolts:bolts-applinks:1.4.0'
    compile 'com.android.support:support-vector-drawable:25.0.0'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.facebook.stetho:stetho:1.4.1'
    compile 'com.uphyca:stetho_realm:2.0.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.tencent.bugly:crashreport_upgrade:latest.release'
    testCompile 'junit:junit:4.12'
}

void renameApk(fileName) {
    setProperty("archivesBaseName", fileName)
}
